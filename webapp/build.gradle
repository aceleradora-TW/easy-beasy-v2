plugins {
    id "com.moowork.node" version "1.3.1"
}

buildDir = "$projectDir/dist"

node {
    version = '12.13.0'
    yarnVersion = '1.19.1'
    download = true
}

def buildCommandForEnvironment = { mode ->
    [
        'vue-cli-service',
        'build',
        '--mode',
        mode
    ]
}

task clean(type: Delete) {
    delete buildDir
}

task yarnInstall(type: YarnTask) {
    args = ['install']
}

task lint(type: YarnTask) {
    args = ['lint']
}

task buildForProduction(dependsOn: [yarnInstall, clean], type: YarnTask) {
    args = buildCommandForEnvironment('production')
}

task buildForDevelopment(type: YarnTask) {
    inputs.dir("$projectDir/src")
    inputs.dir("$projectDir/public")
    inputs.file("$projectDir/babel.config.js")
    inputs.file("$projectDir/package.json")

    outputs.dir buildDir

    args = buildCommandForEnvironment('development')
}

